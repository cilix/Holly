The Holly Programming Language

types: String, Number, Object, Array, Boolean
binary operators: | - + * ^ / % >> << & 
compound: |= -= += *= ^= /= %= >>= <<= &= 
unary operator: ! ~ * -
comparison: < > == != <= >= and or
reserved symbols: \ { } [ ] : = ( ) ; , " ' -- :: ..
other reserved words: let if else return while fn true false nil for in

GRAMMER - BNF

comparison ::=
  LessThan |
  GreaterThan |
  EqualTo |
  NotEqualTo |
  LessThanOrEqualTo |
  GreaterThanOrEqualTo |
  And |
  Or
 
arithmetic ::=
  LogicalOr |
  Subtract |
  Add |
  Multiply |
  Xor |
  Divide |
  Mod |
  RightShift |
  LeftShift |
  LogicalAnd

binop ::=
  arithmetic |
  comparison

unop ::=
  BooleanNot |
  LogicalNot |
  WeakRef |
  Negate 

literal ::=
  Number |
  Boolean |
  Nil     

expression ::=
  literal |
  value |
  unop expression |
  expression binop expression |
  `(` expression `)` 
  
value ::=
  object access |
  array access |
  Name access |
  lambda access
  
access ::=
  `.` Name access |
  `[` expressions `]` access |
  functioncallend access |
  nil

block ::=
  `{` statementlist `}`

lambda ::=
  `fn` namelist block | 
  `fn` namelist `->` expression 
  
functioncallend ::=
  `(` expressionlist `)`
  
expressionlist ::=
  expression |
  expression `,` expressionlist |
  nil
  
namelist ::=
  name |
  name `,` namelist |
  nil
  
name ::= 
  Name |
  Name typehint
  
typehint ::=
  `:` Type
  
ifstatement ::=
  `if` expression block elsestatement 
  
elsestatement ::=
  `else` block |
  `else` ifstatement |
  nil
  
whilestatement ::=
  `while` expression block 
  
forstatement ::=
  `for` Name [ `,` Name ] `in` iterable block |         
  `for` Name [ `,` Name ] `in` iterable `:` statement 
  
iterable ::=
  spread |
  Object |
  Array |
  String
  
spread ::=
  Number `..` Number  

object ::=
  `{` pairlist `}`
  
array ::=
  `[` expressionlist `]`
  
pairlist ::=
  Name `:` expression |
  Name `:` expression `,` pairlist |
  nil
  
statement ::=
  ifstatement |
  whilestatement |
  `return` expression |
  `break` |
  `let` Name `=` expression |
  value `=` expression |        *value must not be a functioncall
  value                         *value must be a functioncall
  
statementlist ::=
  statement [ `;` ] statementlist | *semicolon is optional
  nil
  
start ::=
  statementlist


  
POSSIBLE GRAMMAR AMBIGUITIES

1. Lack of semicolon issues

if x > 2 : return

something()

There's no way of knowing whether or not we're supposed to return
the result of the function call. A possible solution is to require that
return returns a value (like nil).  


OTHER RANDOM NOTES

I will probably get rid of integers at least for the first version. IEEE-754
floating point numbers are far simpler to deal with.

CODE SAMPLES

let Person = fn name, age {
  return { 
    name: name,
    age: age,
    speak: fn self {
      print('Hello, my name is ' + self.name)
    }
  }
}

-- or, sugar for functions returning a single expression
let Person = fn name, age -> 
  { name: name, age: age, 
    speak: fn self -> 
      print('hello, my name is ' + self.name) }

let bob = Person('bob', 25)

bob.speak(bob)

-- or, sugar for implicitly passing the object as the first value
bob::speak()

let add = fn x, y -> x + y

let a = add(2, 3)

if a > 4 {
  let x = min(
}

